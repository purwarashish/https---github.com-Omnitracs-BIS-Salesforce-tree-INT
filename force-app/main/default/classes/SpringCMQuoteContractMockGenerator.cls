/********************************************************************************************
 Generated by: Dennis Le
 Date: 7/31/2018
 Purpose: use for testing the class SpringCMQuoteContractCallOut.cls
   Test class requires to implements HttpCalloutMock.class for api callout
********************************************************************************************/
@isTest
public class SpringCMQuoteContractMockGenerator implements HttpCalloutMock {
	protected Integer code;
	public String status;
	protected String bodyAsString;
	protected Blob bodyAsBlob;
	protected Map<String, String> responseHeaders;

	public SpringCMQuoteContractMockGenerator (Integer code, String status, String body,
    		                                     Map<String, String> responseHeaders) 
	{
		this.code = code;
		this.status = status;
		this.bodyAsString = body;
		this.bodyAsBlob = null;
		this.responseHeaders = responseHeaders;
	}

	public SpringCMQuoteContractMockGenerator (Integer code, String status, Blob body,
    		                                     Map<String, String> responseHeaders) 
	{
		this.code = code;
		this.status = status;
		this.bodyAsBlob = body;
		this.bodyAsString = null;
		this.responseHeaders = responseHeaders;
	}

	public HTTPResponse respond(HTTPRequest req) {
		HttpResponse resp = new HttpResponse();
		resp.setStatusCode(code);
		resp.setStatus(status);
		if (bodyAsBlob != null) {
			resp.setBodyAsBlob(bodyAsBlob);
		} else {
			resp.setBody(bodyAsString);
		}

		if (responseHeaders != null) {
			for (String key : responseHeaders.keySet()) {
				resp.setHeader(key, responseHeaders.get(key));
			}
		}
		return resp;
	}
}